// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'
buildscript {
    ext.versions = [
        'minSdk' : 14,
        'compileSdk' : 26,
        'targetSdk' : 26,
        'buildTools' : '26.0.1',
        'supportLibrary' : '26.1.0',
        'androidPlugin': '3.0.0-beta5',
        'androidTools': '26.0.0-beta5',
        'kotlin': '1.1.4-3',
    ]

    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

project.ext.isDefaultEnvironment = !project.hasProperty('overrideBuildEnvironment')
File getEnvironmentScript()
{
  final File env = file(isDefaultEnvironment ? 'defaultEnvironment.gradle' : project.overrideBuildEnvironment)
  assert env.isFile() : "The environment script [$env] does not exists or is not a file."
  return env
}
apply from: environmentScript

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

// Turn off doclint when using Java 8 (otherwise, you'll see errors when running "gradle androidJavadocs")
if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}
