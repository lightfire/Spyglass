apply plugin: 'com.android.library'
apply plugin: 'idea'
apply plugin: 'maven'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
        checkReleaseBuilds false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }

    testOptions.unitTests.all {
        // configure the set of classes for JUnit tests
        include '**/*Test.class'
        exclude '**/espresso/**/*.class'

        // configure max heap size of the test JVM
        maxHeapSize = '2048m'

        // configure the test JVM arguments
        jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

        // Specify max number of processes (default is 1)
        maxParallelForks = 4

        // Specify max number of test classes to execute in a test process
        // before restarting the process (default is unlimited)
        forkEvery = 150

        // configure whether failing tests should fail the build
        ignoreFailures false

        // use afterTest to listen to the test execution results
        afterTest { descriptor, result ->
            println "Executing test for ${descriptor.name} with result: ${result.resultType}"
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    compile "com.android.support:support-v4:${versions.supportLibrary}"
    compile "com.android.support:support-compat:${versions.supportLibrary}"
    compile "com.android.support:support-annotations:${versions.supportLibrary}"
    compile "com.android.support:appcompat-v7:${versions.supportLibrary}"
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

//apply from: 'gradle-mvn-push.gradle'
